plugins {
    id 'groovy'
    id 'nebula.release' version '16.1.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'io.github.artemy-osipov.thrift'

allprojects {
    apply plugin: 'java-library'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenCentral()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'org.apache.thrift:libthrift:0.16.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.codehaus.groovy:groovy-test:3.0.12'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.35.0'
    testImplementation project(':thrift-example')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER'))
            password.set(System.getenv('SONATYPE_TOKEN'))
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    def signingKey = System.getenv('GPG_KEY')
    def signingPassword = System.getenv('GPG_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'jackson-datatype-thrift'
                description = 'Jackson datatype module to support JSON serialization/deserialization of Thrift objects'
                url = 'https://github.com/artemy-osipov/jackson-datatype-thrift'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'artemy-osipov'
                        name = 'Artemy Osipov'
                        email = 'osipov.artemy@gmail.com'
                    }
                }
                scm {
                    connection = 'git@github.com:artemy-osipov/jackson-datatype-thrift.git'
                    developerConnection = 'git@github.com:artemy-osipov/jackson-datatype-thrift.git'
                    url = 'https://github.com/artemy-osipov/jackson-datatype-thrift'
                }
            }
        }
    }
}
